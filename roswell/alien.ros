#!/bin/sh
#|-*- mode:lisp -*-|#
#|
exec ros dynamic-space-size=8000 -L sbcl-bin -m alien -Q -- $0 "$@"
|#
(progn ;;init forms
  (ros:ensure-asdf)
  #+quicklisp (ql:quickload '(:strips) :silent t))

(in-package :strips)

(defun show-doc (specs)
  (iter (for (key arg variable) in specs)
        (format *error-output*
                "~@{~40@a : ~a~%~}"
                (format nil "~(~{~a~^ | ~}~) ~a" (ensure-list key) arg)
                (or (documentation variable 'variable) "undocumented")
                ""
                (format nil "  default value: ~a"
                        (if (boundp variable)
                            (symbol-value variable)
                            "unspecified")))))

(defvar *search-option* '(eager (bucket-open-list (ff/rpg)))
  "search configurations.")

(defun main-search ()
  (with-memory-usage-diff ()
    (run
     (timeout
      (- *time-limit*
         ;; subtract the current runtime
         (/ (- (get-internal-real-time) *start-time*)
            internal-time-units-per-second))
      (eval *search-option*)))))

(defun main (&rest argv)
  (declare (ignorable argv))
  (ematch argv
    ((list* "-t" (read *time-limit*) rest)
     (apply #'main rest))
    ((list* "-m" (read *memory-limit*) rest)
     (apply #'main rest))
    ((list* "--sg-compiled-branch-limit" (read *sg-compiled-branch-limit*) rest)
     (apply #'main rest))
    ((list* "--search-option" (read *search-option*) rest)
     (apply #'main rest))
    
    ((list problem)
     (print-plan
      (solve-once
       (find-domain problem) problem #'main-search)))
    
    ((list domain problem)
     (print-plan
      (solve-once domain problem #'main-search)))
    
    ((list domain problem plan-output-file)
     (solve-once-to-file
      domain problem plan-output-file #'main-search))
    (_
     (format *error-output* "Usage: alien.ros [options] [domain] problem [plan-output-file]~%")
     (format *error-output* "Got ARGV: ~a~%" argv)
     (show-doc
      `((-t sec *time-limit*)
        (-m kbyte *memory-limit*)
        (--sg-compiled-branch-limit limit *sg-compiled-branch-limit*)
        (--effect-compilation-threashold limit *effect-compilation-threashold*)
        (--search-option SEXP *search-option*))))))
;;; vim: set ft=lisp lisp:
